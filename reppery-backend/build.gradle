buildscript {
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:${sonarcubeGradlePluginVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'
apply plugin: 'org.sonarqube'

group = 'pl.szotaa'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
}

dependencies {
	compile("org.springframework.boot:spring-boot-starter-web:${springBootVersion}")
	compile("org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}")
	compile("org.springframework.boot:spring-boot-starter-security:${springBootVersion}")
	compile("org.springframework.boot:spring-boot-devtools:${springBootVersion}")
	compile("org.postgresql:postgresql:${postgresqlVersion}")
	compile("com.h2database:h2:${h2Version}")
	compile("org.projectlombok:lombok:${lombokVersion}")
	compile("io.jsonwebtoken:jjwt:${jjwtVersion}")
	compile("io.springfox:springfox-swagger2:${springFoxVersion}")
	compile("com.sendgrid:sendgrid-java:${sendGridVersion}")
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile("org.codehaus.groovy:groovy-all:${groovyVersion}")
	testCompile("org.spockframework:spock-core:${spockCoreVersion}")
	testCompile("org.spockframework:spock-spring:1.1-groovy-2.4")
}

jacocoTestReport {
	reports {
		xml.enabled = false
		csv.enabled = false
		html.destination file("${buildDir}/jacocoReportHtml")
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.5
			}
		}
	}
}

task cleanStaticResources(type: Delete){
	group 'cleanup'
	description 'Removes everything in reppery-backend/src/main/resources/static folder.'
	delete staticResourcesPath
}

task updateEmbeddedAngularClient(type: Copy){
	group 'other'
	description 'Copies prebuilt Angular client to reppery-backend/src/main/resources/static folder.'
	from file(angularProdBuildPath)
	into file(staticResourcesPath)
}

task serve(type: Exec){
	group 'dev'
	description 'Runs Spring Boot with dev profile enabled.'
	dependsOn bootRun
}

bootRun {
	args = ["--spring.profiles.active=${bootRunSpringProfile}"]
}